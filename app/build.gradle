apply plugin: 'io.sentry.android.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.example.barcodescanner"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 10
        versionName "1.8"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary  true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental":"true"]
            }
        }

        buildConfigField "boolean", "ERROR_REPORTS_ENABLED_BY_DEFAULT", "true"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8.toString()
        targetCompatibility = JavaVersion.VERSION_1_8.toString()
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "market"
    productFlavors {
        appGallery {}
        googlePlay {
            applicationId "org.barcodescanner"
        }
        fDroid {
            buildConfigField "boolean", "ERROR_REPORTS_ENABLED_BY_DEFAULT", "false"
        }
    }

    sentry {
        autoProguardConfig true
        autoUpload true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Android
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Room
    final roomVersion = "2.2.5"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Paging
    final pagingVersion = "1.0.1"
    implementation "android.arch.paging:runtime:$pagingVersion"
    implementation "android.arch.paging:rxjava2:$pagingVersion"

    // Barcode Scanner
    implementation 'com.budiyev.android:code-scanner:2.1.0'

    // Barcode Image Generator
    //noinspection GradleDependency
    implementation 'com.journeyapps:zxing-android-embedded:3.4.0'

    // VCard Parser
    implementation 'com.googlecode.ez-vcard:ez-vcard:0.11.0'

    // Base32 Encoder
    implementation "commons-codec:commons-codec:1.15"

    // OTP
    implementation "dev.turingcomplete:kotlin-onetimepassword:2.0.1"

    // Rx
    implementation "io.reactivex.rxjava2:rxkotlin:2.3.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"

    // Image Crop Library
    implementation 'com.isseiaoki:simplecropview:1.1.8'

    // Date time picker
    implementation 'com.github.florent37:singledateandtimepicker:2.2.6'

    // Sentry
    implementation 'io.sentry:sentry-android:2.3.1'
}
